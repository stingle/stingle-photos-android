apply plugin: 'com.android.application'

// Creates a variable called keystorePropertiesFile, and initializes it to the
// keystore.properties file.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initializes a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Loads the keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            keyAlias keystoreProperties['keyAlias']
            storePassword keystoreProperties['storePassword']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    defaultConfig {
        applicationId "org.stingle.photos"
        minSdkVersion 23
        targetSdkVersion 34
        compileSdk 34
        versionCode 57
        versionName "2.11.0"
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            resValue "string", "app_name", "Stingle Photos"
            resValue "string", "camera", "Camera"
            resValue "string", "content_provider", "org.stingle.photos.share"
            resValue "string", "api_server_url", "https://api.stingle.org/"
            resValue "string", "default_home_folder_name", "StinglePhotos"
            buildConfigField "Boolean", "ALLOW_INSECURE_TLS", 'false'
            debuggable false
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            resValue "string", "content_provider", "org.stingle.photos.share.debug"
            resValue "string", "app_name", "Stingle Photos Dev"
            resValue "string", "camera", "Camera Dev"
            resValue "string", "api_server_url", "https://api.stingle.org/"
            resValue "string", "default_home_folder_name", "StinglePhotosDev"
            buildConfigField "Boolean", "ALLOW_INSECURE_TLS", 'true'
            debuggable true
        }
    }
    productFlavors {
        playstore {
            dimension 'version'
        }
        fdroid {
            dimension 'version'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    ndkVersion '21.3.6528147'
    flavorDimensions 'version'

    buildFeatures {
        viewBinding true
    }
    namespace 'org.stingle.photos'
}

dependencies {
    implementation "com.goterl:lazysodium-android:5.0.2@aar"
    implementation 'net.java.dev.jna:jna:5.10.0@aar'
    implementation 'com.google.android.exoplayer:exoplayer:2.7.3'
    implementation 'androidx.core:core:1.9.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.exifinterface:exifinterface:1.3.6'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation "androidx.biometric:biometric:1.1.0"
    implementation 'com.google.android:flexbox:2.0.1'
    implementation "androidx.work:work-runtime:2.8.1"
    implementation project(path: ':picasso')

    playstoreImplementation 'com.android.billingclient:billing:5.2.1'

    // CameraX
    implementation "androidx.camera:camera-core:1.1.0-alpha12"
    implementation "androidx.camera:camera-camera2:1.1.0-alpha12"
    implementation "androidx.camera:camera-lifecycle:1.1.0-alpha12"
    implementation 'androidx.camera:camera-view:1.0.0-alpha32'
    implementation 'androidx.camera:camera-video:1.1.0-alpha12'
    implementation 'androidx.camera:camera-extensions:1.0.0-alpha32'
    implementation 'androidx.window:window:1.0.0-alpha09'

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'
}
